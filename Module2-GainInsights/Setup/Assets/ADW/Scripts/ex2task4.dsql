-- Load data into a table using CTAS
-- Create a partitioned table
CREATE SCHEMA [adw]
GO

CREATE TABLE adw.FactProductLog
WITH (
	CLUSTERED COLUMNSTORE INDEX,
	DISTRIBUTION = HASH(ProductID),
    PARTITION   (   LogDate RANGE RIGHT FOR VALUES
                   ( 20160704, 20160705, 20160706)
                    )
	)
AS 
SELECT
	LogDate,
	ProductID,
	Title, 
	Category,
	ProdType,
	TotalClicked
FROM asb.ProductLogExternal
GO

CREATE STATISTICS Stat_adw_FactProductLog_LogDate on adw.FactProductLog(LogDate) WITH FULLSCAN;
CREATE STATISTICS Stat_adw_FactProductLog_ProductID on adw.FactProductLog(ProductID);
CREATE STATISTICS Stat_adw_FactProductLog_category on adw.FactProductLog(category);
CREATE STATISTICS Stat_adw_FactProductLog_title on adw.FactProductLog(title);
CREATE STATISTICS Stat_adw_FactProductLog_prodtype on adw.FactProductLog(prodtype);
CREATE STATISTICS Stat_adw_FactProductLog_totalclicked on adw.FactProductLog(totalclicked);

-- View the distribution of rows across the partitions
DBCC SHOW_STATISTICS ([adw.FactProductLog], Stat_adw_FactProductLog_LogDate) WITH histogram--, density_vector

SELECT  QUOTENAME(s.[name])+'.'+QUOTENAME(t.[name]) as Table_name
,       i.[name] as Index_name
,       p.partition_number as Partition_nmbr
,       p.[rows] as Row_count
,       p.[data_compression_desc] as Data_Compression_desc
FROM    sys.partitions p
JOIN    sys.tables     t    ON    p.[object_id]   = t.[object_id]
JOIN    sys.schemas    s    ON    t.[schema_id]   = s.[schema_id]
JOIN    sys.indexes    i    ON    p.[object_id]   = i.[object_Id]
                            AND   p.[index_Id]    = i.[index_Id]
WHERE t.[name] = 'FactProductLog' AND s.[name]='ADW'

SELECT COUNT(*), LogDate
FROM adw.FactProductLog
GROUP BY LogDate


